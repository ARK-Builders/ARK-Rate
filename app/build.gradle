plugins {
    id 'com.android.application'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "space.taran.arkrate"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        setProperty("archivesBaseName", "ark-rate")

        def login = System.getenv("ACRA_LOGIN") ?: ""
        def password = System.getenv("ACRA_PASS") ?: ""
        def uri = System.getenv("ACRA_URI") ?: ""
        buildConfigField "String", "ACRA_LOGIN", "\"$login\""
        buildConfigField "String", "ACRA_PASS", "\"$password\""
        buildConfigField "String", "ACRA_URI", "\"$uri\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        testRelease {
            storeFile project.rootProject.file('./testRelease.jks')
            storePassword "arkrate"
            keyAlias "key0"
            keyPassword "arkrate"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher_debug",
                    appLabel: "@string/app_name_debug"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.testRelease
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher",
                    appLabel: "@string/app_name"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.2"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.27.0"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.5.1'

    implementation "com.google.dagger:dagger:2.42"
    kapt "com.google.dagger:dagger-compiler:2.42"

    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    implementation 'ch.acra:acra-http:5.9.6'
    implementation 'ch.acra:acra-dialog:5.9.6'
    implementation 'com.github.SimpleMobileTools:Simple-Commons:7e0240b1e3'
}
